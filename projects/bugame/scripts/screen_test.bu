print("Load Main");
import math;
include "stages.bu";

var KEY_UP = 265;
var KEY_DOWN = 264;
var KEY_RETURN = 257;
var KEY_ESCAPE = 256;

var WINDOW_WIDTH = 900;
var WINDOW_HEIGHT = 600;

var POPUP_NONE = 0;
var POPUP_EXIT_APP = 1;
var POPUP_BACK_MENU = 2;
var STAGE_MENU = 10;
var STAGE_GAME = 20;
var STAGE_CREDITS = 30;

var menu_index = 0;
var menu_hover = -1;
var menu_items_t = 0.0;

var popup_action = POPUP_NONE;
var popup_title = "";
var popup_msg = "";
var popup_open = false;
var popup_just_opened = false;
var popup_anim = 0.0;
var popup_tween = Tween(0, 0, 0.001, "linear");
var exit_fade = 0.0;
var exit_closing = false;
var exit_fade_tween = Tween(0, 0, 0.001, "linear");

def point_in_rect(px, py, rx, ry, rw, rh)
{
    return (px >= rx && px <= rx + rw && py >= ry && py <= ry + rh);
}

def open_popup(_action, _title, _msg)
{
    popup_action = _action;
    popup_title = _title;
    popup_msg = _msg;
    popup_open = true;
    popup_just_opened = true;
    popup_tween.start(popup_anim, 1.0, 0.22, "back_out");
}

def close_popup()
{
    popup_open = false;
    popup_just_opened = false;
    popup_tween.start(popup_anim, 0.0, 0.16, "sine_in");
}

class MenuScreen : Screen
{
    var intro;
    var leaving;
    var next_stage;

    def init()
    {
        super.init("Menu");
        self.set_stage(STAGE_MENU);
        self.intro = Tween(0, 1, 0.001, "linear");
        self.leaving = false;
        self.next_stage = -1;
    }

    def enter()
    {
        self.leaving = false;
        self.next_stage = -1;
        self.intro.start(0, 1, 0.65, "sine_out");
        menu_items_t = 0.0;
    }

    def start_leave(_stage)
    {
        if (self.leaving)
        {
            return;
        }

        self.leaving = true;
        self.next_stage = _stage;
        self.intro.start(menu_items_t, 0, 0.32, "sine_in");
    }

    def update(_dt)
    {
        var t = self.intro.update(_dt);
        menu_items_t = t;

        set_color(24, 27, 36);
        draw_rectangle(0, 0, WINDOW_WIDTH, WINDOW_HEIGHT, true);

        set_color(255, 235, 120);
        draw_text("BU GAME", 44, 40 + (1 - t) * 24, 46);

        set_color(210, 215, 225);
        draw_text("Menu demo com Stages + Popup", 46, 94 + (1 - t) * 16, 22);

        var version_t = t - 0.24;
        if (version_t < 0)
        {
            version_t = 0;
        }
        if (version_t > 1)
        {
            version_t = 1;
        }

        var by_t = t - 0.36;
        if (by_t < 0)
        {
            by_t = 0;
        }
        if (by_t > 1)
        {
            by_t = 1;
        }

        set_alpha(int(255 * version_t));
        set_color(170, 178, 196);
        var version_text = "Version 0.1.0";
        var version_size = 16;
        var version_w = get_text_width(version_text, version_size);
        var version_x = WINDOW_WIDTH - 20 - version_w + (1 - version_t) * 38;
        draw_text(version_text, version_x, WINDOW_HEIGHT - 28 + (1 - version_t) * 14, version_size);

        set_alpha(int(255 * by_t));
        set_color(200, 210, 228);
        var by_text = "by Luis Santos AKA djoker";
        var by_size = 16;
        draw_text(by_text, 20 - (1 - by_t) * 38, WINDOW_HEIGHT - 28 + (1 - by_t) * 14, by_size);
        set_alpha(255);

        if (self.leaving && self.intro.done())
        {
            self.leaving = false;
            self.goto_stage(self.next_stage);
        }
    }
}

class GameScreen : Screen
{
    var intro;
    var timer = 0.0;

    def init()
    {
        super.init("Game");
        self.set_stage(STAGE_GAME);
        self.intro = Tween(0, 1, 0.001, "linear");
    }

    def enter()
    {
        self.timer = 0.0;
        self.intro.start(0, 1, 0.28, "sine_out");
    }

    def update(_dt)
    {
        self.timer += _dt;
        var t = self.intro.update(_dt);

        set_color(20, 35, 26);
        draw_rectangle(0, 0, WINDOW_WIDTH, WINDOW_HEIGHT, true);

        var stripe = 120 + (sin(self.timer * 2.8) + 1) * 55;
        set_color(40, 85, 52);
        draw_rectangle(0, stripe, WINDOW_WIDTH, 52, true);

        set_color(230, 245, 230);
        draw_text("STATE: GAME", 44, 42 + (1 - t) * 14, 34);
        draw_text("ESC abre popup para voltar ao menu", 44, 82 + (1 - t) * 10, 20);
    }
}

class CreditsScreen : Screen
{
    var intro;

    def init()
    {
        super.init("Credits");
        self.set_stage(STAGE_CREDITS);
        self.intro = Tween(0, 1, 0.001, "linear");
    }

    def enter()
    {
        self.intro.start(0, 1, 0.35, "sine_out");
    }

    def update(_dt)
    {
        var t = self.intro.update(_dt);

        set_color(18, 22, 34);
        draw_rectangle(0, 0, WINDOW_WIDTH, WINDOW_HEIGHT, true);

        set_color(255, 230, 120);
        draw_text("CREDITS", 44, 42 + (1 - t) * 18, 38);

        set_color(220, 225, 235);
        draw_text("Bu Language Demo", 46, 100 + (1 - t) * 12, 24);
        draw_text("Engine + Script flow", 46, 132 + (1 - t) * 12, 20);
        draw_text("CLICK ou ESC para voltar", 46, 166 + (1 - t) * 12, 18);
    }
}

set_window_size(WINDOW_WIDTH, WINDOW_HEIGHT);
set_window_title("Bu Stages Demo");

var stages = Stages();
stages.add(MenuScreen());
stages.add(GameScreen());
stages.add(CreditsScreen());
stages.change(STAGE_MENU);

loop
{
    var dt = delta();
    

    var mx = get_mouse_x();
    var my = get_mouse_y();

    stages.update(dt);
    popup_anim = popup_tween.update(dt);
    if (exit_closing)
    {
        exit_fade = exit_fade_tween.update(dt);
        if (exit_fade_tween.done())
        {
            close_window();
            break;
        }
    }

    var stage_id = -1;
    if (stages.current != nil)
    {
        stage_id = stages.current.stage;
    }

    if (!popup_open && popup_anim < 0.001 && popup_action != POPUP_NONE)
    {
        popup_action = POPUP_NONE;
        popup_title = "";
        popup_msg = "";
    }

    if (!popup_open && !exit_closing)
    {
        if (stage_id == STAGE_MENU)
        {
            var btn_x = WINDOW_WIDTH / 2 - 120;
            var btn_w = 240;
            var btn_h = 42;
            var base_y = 240;
            var gap = 54;
            var menu_t = menu_items_t;

            if (menu_t < 0)
            {
                menu_t = 0;
            }
            if (menu_t > 1)
            {
                menu_t = 1;
            }
            var menu_ready = (menu_t > 0.85);

            menu_hover = -1;
            for (var i = 0; i < 3; i += 1)
            {
                var item_t = menu_t - i * 0.16;
                if (item_t < 0)
                {
                    item_t = 0;
                }
                if (item_t > 1)
                {
                    item_t = 1;
                }
                var by = base_y + i * gap + (1 - item_t) * 36;
                if (item_t > 0.6 && point_in_rect(mx, my, btn_x, by, btn_w, btn_h))
                {
                    menu_hover = i;
                }
            }

            if (menu_hover != -1)
            {
                menu_index = menu_hover;
            }

            if (key_pressed(KEY_UP))
            {
                menu_index -= 1;
                if (menu_index < 0)
                {
                    menu_index = 2;
                }
            }
            if (key_pressed(KEY_DOWN))
            {
                menu_index += 1;
                if (menu_index > 2)
                {
                    menu_index = 0;
                }
            }

            var activate = false;
            if (menu_ready && key_pressed(KEY_RETURN))
            {
                activate = true;
            }
            if (menu_ready && mouse_pressed(0) && menu_hover != -1)
            {
                menu_index = menu_hover;
                activate = true;
            }

            for (var j = 0; j < 3; j += 1)
            {
                var item_t = menu_t - j * 0.16;
                if (item_t < 0)
                {
                    item_t = 0;
                }
                if (item_t > 1)
                {
                    item_t = 1;
                }

                if (item_t > 0.01)
                {
                    var y = base_y + j * gap + (1 - item_t) * 36;
                    set_alpha(int(255 * item_t));

                    if (menu_index == j)
                    {
                        set_color(88, 120, 170);
                    }
                    else
                    {
                        set_color(52, 62, 84);
                    }
                    draw_rectangle(btn_x, y, btn_w, btn_h, true);

                    set_color(220, 225, 235);
                    draw_rectangle(btn_x, y, btn_w, btn_h, false);

                    if (j == 0)
                    {
                        draw_text("PLAY", btn_x + 86, y + 10, 24);
                    }
                    if (j == 1)
                    {
                        draw_text("CREDITS", btn_x + 62, y + 10, 24);
                    }
                    if (j == 2)
                    {
                        draw_text("EXIT", btn_x + 90, y + 10, 24);
                    }
                }
            }
            set_alpha(255);

            if (activate)
            {
                if (menu_index == 0)
                {
                    stages.current.start_leave(STAGE_GAME);
                }
                elif (menu_index == 1)
                {
                    stages.current.start_leave(STAGE_CREDITS);
                }
                else
                {
                    open_popup(POPUP_EXIT_APP, "Confirm Exit", "Sair da demo?");
                }
            }
        }
        elif (stage_id == STAGE_CREDITS)
        {
            var back_x = 44;
            var back_y = WINDOW_HEIGHT - 78;
            var back_w = 220;
            var back_h = 40;
            var over_back = point_in_rect(mx, my, back_x, back_y, back_w, back_h);

            if (over_back)
            {
                set_color(95, 110, 160);
            }
            else
            {
                set_color(62, 70, 105);
            }
            draw_rectangle(back_x, back_y, back_w, back_h, true);
            set_color(220, 225, 235);
            draw_rectangle(back_x, back_y, back_w, back_h, false);
            draw_text("BACK TO MENU", back_x + 28, back_y + 10, 20);

            if (mouse_pressed(0) || key_pressed(KEY_ESCAPE))
            {
                if (stages.current != nil)
                {
                    stages.current.goto_stage(STAGE_MENU);
                }
                else
                {
                    stages.change(STAGE_MENU);
                }
            }
        }
        elif (stage_id == STAGE_GAME)
        {
            if (key_pressed(KEY_ESCAPE))
            {
                open_popup(POPUP_BACK_MENU, "Pause", "Voltar ao menu?");
            }
        }
    }

    if (popup_anim > 0.001)
    {
        set_color(0, 0, 0);
        set_alpha(int(170 * popup_anim));
        draw_rectangle(0, 0, WINDOW_WIDTH, WINDOW_HEIGHT, true);
        set_alpha(255);

        var box_w = 430;
        var box_h = 165;
        var box_x = WINDOW_WIDTH / 2 - box_w / 2;
        var box_y = WINDOW_HEIGHT / 2 - box_h / 2 + (1 - popup_anim) * 80;

        set_color(34, 36, 44);
        draw_rectangle(box_x, box_y, box_w, box_h, true);
        set_color(220, 220, 225);
        draw_rectangle(box_x, box_y, box_w, box_h, false);

        set_color(255, 255, 255);
        draw_text(popup_title, box_x + 20, box_y + 20, 28);
        draw_text(popup_msg, box_x + 20, box_y + 64, 20);

        var yes_x = box_x + 24;
        var no_x = box_x + box_w - 174;
        var btn_y = box_y + box_h - 54;
        var btn_w = 150;
        var btn_h = 34;

        var over_yes = point_in_rect(mx, my, yes_x, btn_y, btn_w, btn_h);
        var over_no = point_in_rect(mx, my, no_x, btn_y, btn_w, btn_h);

        if (over_yes)
        {
            set_color(76, 120, 84);
        }
        else
        {
            set_color(56, 88, 64);
        }
        draw_rectangle(yes_x, btn_y, btn_w, btn_h, true);
        if (over_no)
        {
            set_color(132, 78, 78);
        }
        else
        {
            set_color(96, 58, 58);
        }
        draw_rectangle(no_x, btn_y, btn_w, btn_h, true);

        set_color(230, 230, 235);
        draw_rectangle(yes_x, btn_y, btn_w, btn_h, false);
        draw_rectangle(no_x, btn_y, btn_w, btn_h, false);
        draw_text("YES", yes_x + 52, btn_y + 8, 20);
        draw_text("CANCEL", no_x + 34, btn_y + 8, 20);

        if (popup_open && popup_anim > 0.95)
        {
            if (popup_just_opened)
            {
                popup_just_opened = false;
            }
            else
            {
            var confirm = false;
            var cancel = false;

            if (key_pressed(KEY_RETURN))
            {
                confirm = true;
            }
            if (key_pressed(KEY_ESCAPE))
            {
                cancel = true;
            }
            if (mouse_pressed(0) && over_yes)
            {
                confirm = true;
            }
            if (mouse_pressed(0) && over_no)
            {
                cancel = true;
            }

            if (confirm)
            {
                if (popup_action == POPUP_EXIT_APP)
                {
                    exit_closing = true;
                    exit_fade_tween.start(exit_fade, 1.0, 0.38, "sine_in");
                    popup_action = POPUP_NONE;
                    popup_title = "";
                    popup_msg = "";
                }
                elif (popup_action == POPUP_BACK_MENU)
                {
                    if (stages.current != nil)
                    {
                        stages.current.goto_stage(STAGE_MENU);
                    }
                    else
                    {
                        stages.change(STAGE_MENU);
                    }
                }
                close_popup();
            }
            elif (cancel)
            {
                close_popup();
            }
            }
        }
    }

    if (exit_fade > 0.001)
    {
        set_color(0, 0, 0);
        set_alpha(int(255 * exit_fade));
        draw_rectangle(0, 0, WINDOW_WIDTH, WINDOW_HEIGHT, true);
        set_alpha(255);
    }

    frame;
}
