print("Shader Vignette Demo");
import math;

var KEY_ESCAPE = 256;
var KEY_F1 = 290;
var KEY_UP = 265;
var KEY_DOWN = 264;

var SCREEN_W = 1280;
var SCREEN_H = 720;

var FX = -1;
var SHOW_HELP = true;
var STRENGTH = 1.05;

def clamp(v, a, b)
{
    if (v < a) return a;
    if (v > b) return b;
    return v;
}

def draw_background(t)
{
    set_color(12, 16, 26);
    set_alpha(255);
    draw_rectangle(0, 0, SCREEN_W, SCREEN_H, true);

    set_color(20, 30, 52);
    set_alpha(255);
    draw_rectangle(0, 120, SCREEN_W, 150, true);

    set_color(18, 25, 42);
    set_alpha(255);
    draw_rectangle(0, 420, SCREEN_W, 180, true);

    var i = 0;
    while (i < 10)
    {
        var x = 90 + i * 120 + sin(t * 1.7 + i * 0.35) * 38;
        var y = 240 + cos(t * 1.3 + i * 0.55) * 120;
        var s = 36 + sin(t * 2.0 + i) * 12;

        set_color(80 + i * 11, 110 + i * 8, 170 + i * 5);
        set_alpha(220);
        draw_circle(x, y, s, true);

        set_color(220, 240, 255);
        set_alpha(170);
        draw_circle(x, y, s * 0.45, true);
        i = i + 1;
    }
}

process shader_vignette_world()
{
    FX = load_shader_auto("scripts/shaders/vignette");

    loop
    {
        if (key_pressed(KEY_UP)) STRENGTH = STRENGTH + 0.07;
        if (key_pressed(KEY_DOWN)) STRENGTH = STRENGTH - 0.07;
        STRENGTH = clamp(STRENGTH, 0.05, 2.4);

        if (key_pressed(KEY_F1)) SHOW_HELP = !SHOW_HELP;

        var t = time();

        if (FX >= 0)
        {
            set_shader(FX);
            set_shader_uniform_vec2(FX, "u_resolution", SCREEN_W, SCREEN_H);
            set_shader_uniform_float(FX, "u_strength", STRENGTH);
        }

        draw_background(t);

        if (FX >= 0)
            reset_shader();

        set_color(245, 245, 245);
        set_alpha(255);
        draw_text("Shader: Vignette post FX", 20, 20, 26);
        draw_text("Strength: " + str(STRENGTH), 20, 52, 20);
        draw_fps(20, 80);

        if (SHOW_HELP)
            draw_text("UP/DOWN strength | F1 help | ESC quit", 20, 108, 18);

        if (key_down(KEY_ESCAPE))
        {
            if (FX >= 0) unload_shader(FX);
            close_window();
            break;
        }

        frame;
    }
}

set_window_size(SCREEN_W, SCREEN_H);
set_window_title("Shader Vignette Demo");
set_design_resolution(SCREEN_W, SCREEN_H);
set_virtual_screen_enabled(true);
set_draw_screen(false);

shader_vignette_world();
